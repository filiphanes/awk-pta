#!/usr/bin/awk -f
# Preprocessor for:
# - tags applied to following lines are defined on lines beginning with =
# - next line beginning with = replaces tags applied
# - empty line clears shared tags
# - dates are recognized only in ISO format YYYY-MM-DD

# Outputs simple greppable, sortable, normalized postings lines,
# without comments and unrecognized lines
# With following fixed fields order:
# date amount currency account other tags

BEGIN {
    ACCOUNT_REGEX = ENVIRON["PTA_ACCOUNT_REGEX"];
    if (!ACCOUNT_REGEX) ACCOUNT_REGEX = "(expenses|assets|liabilities|income):[^ ]+";
    ACCOUNT_REGEX = " " ACCOUNT_REGEX;

    COMMODITY_REGEX = ENVIRON["PTA_COMMODITY_REGEX"];
    if (!COMMODITY_REGEX) COMMODITY_REGEX = "[A-Z]+";

    DEFAULT_COMMODITY = ENVIRON["PTA_DEFAULT_COMMODITY"];
    if (!DEFAULT_COMMODITY) DEFAULT_COMMODITY = "USD";
}

function extractDate() {
    if (match($0, / [0-9]{4}-[0-9]{2}(-[0-9]{2})? /) > 0) {
        d = substr($0, RSTART+1, RLENGTH-1)
        # remove it
        $0 = substr($0, 1, RSTART) substr($0, RSTART+RLENGTH)
        return d
    } else
        return date
}

function extractAccount() {
    if (match($0, ACCOUNT_REGEX) > 0) {
        a = substr($0, RSTART, RLENGTH)
        # remove it
        $0 = substr($0, 1, RSTART-1) substr($0, RSTART+RLENGTH)
        return a
    } else 
        return account
}

# date is first
/^[0-9]{4}-[0-9]{2}(-[0-9]{2})?/ {
    myaccount = extractAccount()
    $2 = $2 myaccount tags
    print
    next
}

# amount is first
/^ *[-+]?[0-9]+(\.[0-9]+)?/ {
    myaccount = extractAccount()
    if (match($2, COMMODITY_REGEX) > 0) {
        $1 = date $1 myaccount #tags
    } else {
        $1 = date $1 " " DEFAULT_COMMODITY myaccount #tags
    }
    print $0 tags
    next
}

# Common tags set by =
/^=/ {
    sub(/^= *| *$/, " ")
    date = ""
    account = ""
    date = extractDate()
    account = extractAccount()
    tags = $0
    next
}

# Empty line resets shared tags
/^ *$/ {
    date = ""
    account = ""
    tags = ""
}

# Lines starting with # are not recognized and thus ignored as comments
