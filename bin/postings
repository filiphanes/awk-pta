#!/usr/bin/env gawk -f
# Preprocessor for:
# - dates are recognized in ISO format YYYY-MM-DD or YYYY-MM
# - if posting date is missing, last date is used or 0000-01-01
# - if amount is not at $1 or $2 after date, then date, account,
#   tags are set for following postings in transaction
# - if first field is matches account regex it is used in following postings in transaction
# - empty line clears shared variables
# - other lines are ignored

# Outputs simple greppable, sortable, normalized postings lines,
# without comments and unrecognized lines
# With following fixed fields order:
# date amount currency account other tags

BEGIN {
    ACCOUNT_REGEX = ENVIRON["PTA_ACCOUNT_REGEX"]
    if (!ACCOUNT_REGEX) ACCOUNT_REGEX = "(expenses|assets|liabilities|income):[^ ]+"
    AMOUNT_REGEX = "^[-+]?[0-9]+(\\.[0-9]+)?$"
    date = "0000-01-01"
    aliases[""] = ""
}

# amount is first
/^ *[-+]?[0-9]+(\.[0-9]+)? / {
    # last date is always set to postings without it
    if ($2 ~ ACCOUNT_REGEX) $2 = $2 tags
    else if ($2 in aliases) $2 = aliases[$2] tags
    else if (account)       $1 = $1 " " account tags
    else                    $1 = $1 tags
    print date " " $0
    next
}

# date is first
/[0-9]{4}-[0-9]{2}(-[0-9]{2})? / {
    date = $1
    if ($2 ~ AMOUNT_REGEX) {
        # print posting only when numeric value is at second field
        if ($3 ~ ACCOUNT_REGEX) $3 = $3 tags
        else if ($3 in aliases) $3 = aliases[$3] tags
        else if (account)       $2 = $2 " " account tags
        else                    $2 = $2 tags
        print
    #} else if ($2 ~ ACCOUNT_REGEX) {
    #    # this line has shared account and tags for following postings
    #    account = $2
    #    $1 = $2 = ""
    #    tags = $0
    #    gsub(/^[[:space:]]|[[:space:]]*$/, "", tags)  # remove first leading space and all traling
    } else {
        # rest is [account] tags
        if ($2 in aliases) $2 = aliases[$2]
        $1 = ""
        tags = $0
        gsub(/[[:space:]]*$/, "", tags)  # remove all traling spaces
    }
    next
}

# Empty line resets shared tags, date is kept
/^ *$/ {
    account = ""
    tags = ""
    next
}

# account on line sets shared for following postings
$1 ~ ACCOUNT_REGEX {
    account = $1
    if (account in aliases) account = aliases[account]
    $1 = ""
    tags = $0
    gsub(/[[:space:]]*$/, "", tags)  # remove all traling spaces
    next
}

/^alias[[:space:]]/ {
    aliases[$2] = $3
    next
}

# Lines starting with # are not recognized and thus ignored as comments
